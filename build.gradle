subprojects {
    buildscript {
        ext {
            springBootVersion = '2.3.1.RELEASE'
        }
        repositories {
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
        toolVersion = "8.34"
    }

    jacoco {
        toolVersion = "0.8.2"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
        testLogging.exceptionFormat = 'full'
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    task testWithReport() {
        dependsOn test
        finalizedBy jacocoTestReport
    }
    dependencies {
        ext {
            kafkaClientVersion = '2.6.0'
            dropwizardVersion = '3.1.3'
            curatorVersion = '5.1.0'
            zookeeperVersion = '3.6.1'
            jacksonVersion = '2.9.8'
        }
    }
}

task startNakadi(type: Exec) {
    commandLine "bash", "-c", "./nakadi.sh start-nakadi"
}

task fullAcceptanceTest(type: Exec) {
    commandLine "bash", "-c", "./nakadi.sh acceptance-tests"
}

task stopNakadi(type: Exec) {
    commandLine "bash", "-c", "./nakadi.sh stop-nakadi"
}

task startStorages(type: Exec) {
    commandLine "bash", "-c", "./nakadi.sh start-storages"
}

task stopStorages(type: Exec) {
    commandLine "bash", "-c", "./nakadi.sh stop-storages"
}

task checkstyle {
    subprojects.each { dependsOn(":${it.name}:checkstyleMain") }
    subprojects.each { dependsOn(":${it.name}:checkstyleTest") }
    dependsOn(":acceptance-test:checkstyleMain")
    dependsOn(":acceptance-test:checkstyleTest")
}

task test {
    subprojects.each { dependsOn(":${it.name}:testWithReport") }
}
